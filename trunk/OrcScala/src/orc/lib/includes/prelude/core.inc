--
-- core.inc -- Orc standard prelude include, fundementals section
-- Project OrcScala
--
-- $Id$
--
-- Copyright (c) 2010 The University of Texas at Austin. All rights reserved.
--
-- Use and redistribution of this file is governed by the license terms in
-- the LICENSE file found in the project's top-level directory and also found at
-- URL: http://orc.csres.utexas.edu/license.shtml .
--

{--
Fundamental sites and operators.

These declarations include both prefix and infix sites (operators).  For
consistency, all declarations are written in prefix form, with the site name
followed by the operands.  When the site name is surrounded in parentheses, as
in <code>(+)</code>, it denotes an infix operator.

For a more complete description of the built-in operators and their syntax, see
the <olink targetdoc="root" targetptr="language.base.operators">Operators</olink> section of the
User Guide.
--}

{-
Basic types
-}
type String = "orc.types.StringType"
type Number = "orc.types.NumberType"
type Boolean = "orc.types.BooleanType"
type Integer = "orc.types.IntegerType"
type Signal = "orc.types.SignalType"

{--
@site let() :: Signal

<sitepropset>
   <siteprop propname="nonblocking"/>
   <siteprop propname="pure"/>
</sitepropset>

When applied to no arguments, return a signal.


@site let(A) :: A
When applied to a single argument, return that argument (behaving as the identity function).

@site let(A, ...) :: (A, ...)
When applied to two or more arguments, return the arguments as a tuple.
--}
site let = "orc.lib.builtin.Let"

{--
@site If(Boolean) :: Signal

<sitepropset>
   <siteprop propname="nonblocking"/>
   <siteprop propname="pure"/>
</sitepropset>

Fail silently if the argument is false. Otherwise return a signal.

Example:
<programlisting language="orc-demo"><![CDATA[
-- Publishes: "Always publishes"
  If(false) >> "Never publishes"
| If(true) >> "Always publishes"]]></programlisting>

--}
site If = "orc.lib.builtin.If"

{--
@site Unless(Boolean) :: Signal

<sitepropset>
   <siteprop propname="nonblocking"/>
   <siteprop propname="pure"/>
</sitepropset>

Fail silently if the argument is true. Otherwise return a signal.

Example:
<programlisting language="orc-demo"><![CDATA[
-- Publishes: "Always publishes"
  Unless(false) >> "Always publishes"
| Unless(true) >> "Never publishes"]]></programlisting>
--}
site Unless = "orc.lib.builtin.Unless"

{- Aliases for different styles of conditional site usage -}
site Ift = "orc.lib.builtin.If"
site IfT = "orc.lib.builtin.If"
site IfTrue = "orc.lib.builtin.If"

site Iff = "orc.lib.builtin.Unless"
site IfF = "orc.lib.builtin.Unless"
site IfFalse = "orc.lib.builtin.Unless"

{--
@site error(String) :: Bot

<sitepropset>   
   <siteprop propname="nonblocking"/>
</sitepropset>

Halt with the given error message.

Example, using <code>error</code> to implement assertions:
<programlisting language="orc-demo"><![CDATA[
def assert(b) =
  if b then signal else error("assertion failed")

-- Fail with the error message: "assertion failed"
assert(false)]]></programlisting>
--}
site error = "orc.lib.util.Error"


{--
@site (+)(Number, Number) :: Number
<code>a+b</code> returns the sum of <code>a</code> and <code>b</code>.
--}
site (+) = "orc.lib.math.Add"

{--
@site (-)(Number, Number) :: Number
<code>a-b</code> returns the value of <code>a</code> minus the value of <code>b</code>.
--}
site (-) = "orc.lib.math.Sub"

{--
@site (0-)(Number) :: Number
Return the additive inverse of the argument.
When this site appears as an operator, it is written in prefix form without the
zero, i.e. <code>(-a)</code>
--}
site (0-) = "orc.lib.math.UMinus"

{--
@site (*)(Number, Number) :: Number
<code>a*b</code> returns the product of <code>a</code> and <code>b</code>.
--}
site (*) = "orc.lib.math.Mult"

{--
@site (**)(Number, Number) :: Number
<code>a ** b</code> returns
<inlineequation><mathphrase>a<superscript>b</superscript></mathphrase></inlineequation>,
i.e. <code>a</code> raised to the <code>b</code>th power.
--}
site (**) = "orc.lib.math.Exponent"

{--
@site (/)(Number, Number) :: Number
<code>a/b</code> returns <code>a</code> divided by <code>b</code>.
If both arguments have integral types, <code>(/)</code> performs integral
division, rounding towards zero. Otherwise, it performs floating-point
division. If <code>b=0</code>, <code>a/b</code> halts with an error.

Example:
<programlisting language="orc-demo"><![CDATA[
  7/3   -- publishes 2
| 7/3.0 -- publishes 2.333...]]></programlisting>
--}
site (/) = "orc.lib.math.Div"

{--
@site (%)(Number, Number) :: Number
<code>a%b</code> computes the remainder of <code>a/b</code>. If <code>a</code>
and <code>b</code> have integral types, then the remainder is given by
the expression <code>a - (a/b)*b</code>. For a full description, see the
<link xmlns:xlink="http://www.w3.org/1999/xlink" xlink:href="http://java.sun.com/docs/books/jls/third_edition/html/expressions.html#15.17.3">Java Language Specification, 3rd edition</link>.
--}
site (%) = "orc.lib.math.Mod"

{--
@site (<:)(Top, Top) :: Boolean
<code>a &lt;: b</code> returns true if <code>a</code> is less than <code>b</code>, and false otherwise.
--}
site (<:) = "orc.lib.comp.Less"

{--
@site (<=)(Top, Top) :: Boolean
<code>a &lt;= b</code> returns true if <code>a</code> is less than or equal to <code>b</code>, and false otherwise.
--}
site (<=) = "orc.lib.comp.Leq"

{--
@site (:>)(Top, Top) :: Boolean
<code>a :> b</code> returns true if <code>a</code> is greater than <code>b</code>, and false otherwise.
--}
site (:>) = "orc.lib.comp.Greater"

{--
@site (>=)(Top, Top) :: Boolean
<code>a >= b</code> returns true if <code>a</code> is greater than or equal to <code>b</code>, and false otherwise.
--}
site (>=) = "orc.lib.comp.Greq"

{--
@site (=)(Top, Top) :: Boolean
<code>a = b</code> returns true if <code>a</code> is equal to <code>b</code>,
and false otherwise.  The precise definition of "equal" depends on the values
being compared, but always obeys the rule that if two values are considered
equal, then one may be substituted locally for the other without affecting the
behavior of the program.

Two values with the same object identity are always considered equal.
In addition, Cor <olink targetdoc="root" targetptr="cor.constants>constant values</olink> and
<olink targetdoc="root" targetptr="cor.data">data structures</olink> are considered equal if their
contents are equal. Other types are free to implement their own equality
relationship provided it conforms to the rules given here.

Note that although values of dUnlesserent types may be compared with
<code>=</code>, the substitutability principle requires that such values are
always considered inequal, i.e. the comparison will return <code>false</code>.
--}
site (=) = "orc.lib.builtin.Eq"

{--
@site (/=)(Top, Top) :: Boolean
<code>a/=b</code> returns false if <code>a=b</code>, and true otherwise.
--}
site (/=) = "orc.lib.comp.Inequal"

{--
@site (~)(Boolean) :: Boolean
Return the logical negation of the argument.
--}
site (~) = "orc.lib.bool.Not"

{--
@site (&&)(Boolean, Boolean) :: Boolean
Return the logical conjunction of the arguments. This is not a short-circuiting
operator; both arguments must be evaluated and available before the result is
computed.
--}
site (&&) = "orc.lib.bool.And"

{--
@site (||)(Boolean, Boolean) :: Boolean
Return the logical disjunction of the arguments. This is not a short-circuiting
operator; both arguments must be evaluated and available before the result is
computed.
--}
site (||) = "orc.lib.bool.Or"

{--
@site (:)[A](A, List[A]) :: List[A]

The list <code>a:b</code> is formed by prepending the element <code>a</code> to
the list <code>b</code>.

Example:
<programlisting language="orc-demo"><![CDATA[
-- Publishes: (3, [4, 5])
3:4:5:[] >x:xs> (x,xs)]]></programlisting>

In patterns, the <code>(:)</code> deconstructor can be applied to a variety of
list-like values such as <code>Array</code>s and Java <code>Iterable</code>s,
in which case it returns the first element of the list-like value, and a new
list-like value (not necessarily of the same type as the original list-like
value) representing the tail.  Modifying the structure of the original
value (e.g. adding an element to an <code>Iterable</code>) may render
old "tail"s unusable, so you should refrain from modifying a value
while you are deconstructing it.  This feature is highly experimental
and will probably change in future versions of the implementation.
--}
site (:) = "orc.lib.builtin.ConsConstructor"

{--
@def abs(Number) :: Number
Return the absolute value of the argument.

@implementation
--}
def abs(Number) :: Number
def abs(x) = if x <: 0 then -x else x

{--
@def signum(Number) :: Number
<code>signum(a)</code> returns <code>-1</code> if <code>a&lt;0</code>,
<code>1</code> if <code>a&gt;0</code>, and <code>0</code> if <code>a=0</code>.

@implementation
--}
def signum(Number) :: Number
def signum(x) =
  if x <: 0 then -1
  else if x :> 0 then 1
  else 0

{--
@def min[A](A,A) :: A
Return the lesser of the arguments. If the arguments
are equal, return the first argument.

@implementation
--}
def min[A](A,A) :: A
def min(x,y) = if y <: x then y else x

{--
@def max[A](A,A) :: A
Return the greater of the arguments. If the arguments
are equal, return the second argument.

@implementation
--}
def max[A](A,A) :: A
def max(x,y) = if x :> y then x else y


{--
@site floor(Number) :: Integer
Return the greatest integer less than
or equal to this number.
--}
site floor = "orc.lib.math.Floor"

{--
@site ceil(Number) :: Integer
Return the least integer greater than
or equal to this number.
--}
site ceil = "orc.lib.math.Ceil"
